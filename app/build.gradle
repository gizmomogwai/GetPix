buildscript {
  repositories {
    mavenCentral()
  }
}

apply plugin: 'com.android.application'

// from http://stackoverflow.com/a/22597752
def passwordFromKeychain = { name ->
  def command = "security find-generic-password -s ${name} -a ${name} -w"
  println("executing command: " + command);
  return command.execute().text.trim();
}

import org.gradle.internal.logging.text.StyledTextOutput;
import org.gradle.internal.logging.text.StyledTextOutputFactory;
import org.gradle.internal.logging.text.StyledTextOutput.Style
def gitVersion = {
  def version = "git describe --dirty".execute().text.trim();
  if (version.indexOf('dirty') != -1) {
    services
      .get(StyledTextOutputFactory)
      .create("dirty")
      .withStyle(Style.Failure)
      .println("ATTENTION dirty build with version: '${version}'")
  }
  return version;
}

android {
  compileSdkVersion 30
  buildToolsVersion "30.0.2"
  defaultConfig {
    applicationId "com.flopcode.getpix"
    minSdkVersion 24
    targetSdkVersion 30
    versionCode 2
    versionName gitVersion()
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  signingConfigs {
    release {
      storeFile file("../release.keystore")
      storePassword passwordFromKeychain("getpix-play-store-keystore-password")
      keyAlias "getpix"
      keyPassword passwordFromKeychain("getpix-play-store-keystore-password")
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFile getDefaultProguardFile('proguard-android.txt')
      proguardFile 'proguard-rules.pro'
      signingConfig signingConfigs.release
      resValue "string", "app_name", "GetPix"
    }
    debug {
      applicationIdSuffix ".debug"
      resValue "string", "app_name", "GetPix-Debug"
    }

  }
}


dependencies {
  implementation project(':lib')
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support:design:28.0.0'
  implementation 'com.android.support:support-v4:28.0.0'
  implementation 'com.android.support:support-media-compat:28.0.0'
  implementation 'com.android.support:animated-vector-drawable:28.0.0'
  implementation 'com.android.support:customtabs:28.0.0'

  implementation 'org.nanohttpd:nanohttpd:2.3.1'

  implementation 'androidx.annotation:annotation:1.1.0'
  implementation 'com.google.android.material:material:1.3.0'

  implementation deps.butterknife
  implementation 'androidx.appcompat:appcompat:1.3.0'
  implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
  annotationProcessor deps.annotationProcessor
}
