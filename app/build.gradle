plugins {
    id 'com.android.application'
}

def gitVersion = {
    def version = "git describe --dirty".execute().text.trim();
    if (version.indexOf('dirty') != -1) {
        logger.warn("ATTENTION dirty build with version: '${version}'")
    }
    return version;
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

def password = localProperties.getProperty("keystore_password")

android {
    namespace 'com.flopcode.getpix'
    compileSdk 33

    defaultConfig {
        applicationId "com.flopcode.getpix"
        minSdk 24
        targetSdk 33
        versionCode 3
        versionName gitVersion()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        playstore {
            storeFile file("../release.keystore")
            storePassword password
            keyAlias "getpix"
            keyPassword password
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.playstore
        }
    }
}

dependencies {
    implementation project(':lib')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
