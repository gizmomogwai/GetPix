buildscript {
  repositories {
    jcenter()
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

// from http://stackoverflow.com/a/22597752
def passwordFromKeychain = { name ->
  def proc = ["security", "find-generic-password", "-s", name, "-a", name, "-w"].execute()
  proc.waitFor()
  def stdout = proc.in.text
  def res = stdout.trim()
  print res
  return res
}

android {
  compileSdkVersion 23
  buildToolsVersion "24.0.3"

  defaultConfig {
    applicationId "com.flopcode.getpix"
    minSdkVersion 23
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }
  signingConfigs {
    release {
      storeFile file("../release.keystore")
      storePassword passwordFromKeychain("play-store-keystore-password")
      keyAlias "getpixk"
      keyPassword passwordFromKeychain("play-store-keystore-password")
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFile getDefaultProguardFile('proguard-android.txt')
      proguardFile 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
}


dependencies {
  compile project(':lib')
  compile 'com.android.support:appcompat-v7:23.4.0'
  compile 'com.android.support:design:23.4.0'
  compile 'org.nanohttpd:nanohttpd:2.3.1'

  compile deps.butterknife
  apt deps.butterknifeCompiler
}
